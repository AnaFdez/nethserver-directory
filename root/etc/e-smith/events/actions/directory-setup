#!/usr/bin/perl

use NethServer::Directory;
use Net::LDAP;

#
# Ensure slapd is running
#
qx(service slapd status >/dev/null);
if ( $? != 0 ) {
    qx(service slapd start);
    sleep 1;
}

#
# Enable nslcd at startup
#
qx(chkconfig nslcd on);
if ( $? != 0 ) {
    warn 'Error activating nslcd at reboot. Is nslcd available?';
}

#
# Connect to LDAP server
#
my $ldap = NethServer::Directory->new;
my $CertificateFile = "/etc/openldap/slapd.pem";
my $NslcdUid = getpwnam('nslcd');
my $LdapInternalSuffix = $ldap->getInternalSuffix(); 
my $LdapDomainSuffix = $ldap->getDomainSuffix(); 

#
# Load the rwm overlay, if missing
#
my $RewriterModule = `rpm -ql openldap-servers | grep 'rwm\.la\$'`;
chomp($RewriterModule);

my $olcModuleSearch = $ldap->search(
    base => 'cn=config',
    filter => "(&(cn=module{0})(olcModuleLoad={0}$RewriterModule))",
    attributes => ['olcModuleLoad']
    );

if ($olcModuleSearch->count() == 0) {
    my $ldapAddResponse = $ldap->add(
	'cn=module,cn=config',
	attrs => [
	    cn => 'module',
	    objectClass => 'olcModuleList',
	    olcModuleLoad => $RewriterModule	
	]
    );
    if($ldapAddResponse->is_error) {
	warn "`$RewriterModule` rwm module load error";
    }
}


#
# Replace some global settings, access control setup:
#
my @configChangeList = (
    ['cn=config',
     replace => [
	 olcPasswordCryptSaltFormat => '$6$%.86s',
	 olcTLSCipherSuite => 'HIGH:MEDIUM:+SSLv2',
	 olcTLSCACertificateFile => $CertificateFile,
	 olcTLSCertificateFile => $CertificateFile,
	 olcTLSCertificateKeyFile => $CertificateFile,
	 olcTLSVerifyClient => 'never',
    ]],
    ['olcDatabase={-1}frontend,cn=config',
     replace => [olcPasswordHash => '{CRYPT}']],
    ['olcDatabase={2}bdb,cn=config',
     replace => [
	 olcSuffix => $LdapInternalSuffix,
	 olcAccess => [
	     qq{to attrs=userPassword 
                  by anonymous auth
                  by self write
                  by dn.exact="cn=pam,$LdapInternalSuffix" peername.path="/var/run/ldapi" write
                  by * break
             },
	     qq{to * 
                  by dn.exact="gidNumber=0+uidNumber=0,cn=peercred,cn=external,cn=auth" manage
                  by dn.exact="cn=libuser,$LdapInternalSuffix" peername.ip="127.0.0.1" write
                  by users ssf=71 read
             },
	 ]
     ]]
    );

foreach(@configChangeList) {
    my $ldapConfigResponse = $ldap->modify(@{$_});
    if($ldapConfigResponse->is_error) {
	warn 'error modifying `' . @{$_}[0] . '` ' . $ldapConfigResponse->error ;
    }
}

#
# Enable relay overlay 
# see man slapd-relay, man slapo-rwm
#
my $olcRelaySearch = $ldap->search(
    base => 'cn=config',
    scope => 'one',
    filter => '(olcDatabase={0}relay)',
    attributes => ['olcSuffix'],
    sizelimit => 1
    );

if($olcRelaySearch->count() == 0) {
    my $ldapAddRelayResponse = $ldap->add(
	'olcDatabase=relay,cn=config',
	attrs => [
	    objectClass => ['olcDatabaseConfig', 'olcRelayConfig', 'olcConfig'],
	    olcDatabase => 'relay',
	    olcSuffix => $LdapDomainSuffix
	]
	);
    if($ldapAddRelayResponse->is_error) {	
	warn 'Error adding relay database: ' . $ldapAddRelayResponse->error;
    } else {
	addOverlayRwm();
    }
} 

sub addOverlayRwm
{

    my $olcRelaySearch = $ldap->search(
	base => 'cn=config',
	scope => 'one',
	filter => '(olcDatabase={0}relay)',
	attributes => ['olcSuffix'],
	sizelimit => 1
	);


    if($olcRelaySearch->count() == 0) {
	warn 'Cannot find the relay database';
	return
    }

    #
    # Search for existing rwm overlay configurations:
    #
    my $relayDnSuffix = $olcRelaySearch->entry(0)->dn();
    my $olcOverlaySearch = $ldap->search(
	base => $relayDnSuffix,
	scope => 'one',
	filter => 'objectClass=olcRwmConfig',
	sizelimit => 1
	);

    if($olcOverlaySearch->count() == 0) {

	my $ldapAddOverlayResponse = $ldap->add(
	    'olcOverlay=rwm,' . $relayDnSuffix,
	    attrs => [
		objectClass => ['olcOverlayConfig', 'olcRwmConfig'],
		olcOverlay => 'rwm',
		olcRwmRewrite => 'rwm-suffixmassage "dc=directory,dc=nh"'
		]
	    );
	if($ldapAddOverlayResponse->is_error) {
	    warn "Error adding rewrite module overlay on `$relayDnSuffix`: " . 
		$ldapAddOverlayResponse;
	}

    }
}


$ldap->unbind;
